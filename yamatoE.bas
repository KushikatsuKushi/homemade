100 '##############################################################################################################
101 'Space Battleship Yamato (English translation), Fujitsu FM-7, September 2022, ported by KUSHIKATSU.
102 'Ported the original PC-8001 version (1980) to the FM-7. Since I ported it faithfully to the original, 
103 'it is hard to tell the difference from the original at a glance.
104 '
105 'The original version information:
106 'Space battleship Yamato, NEC PC-8001, ASCII Magazine, February 1980.
107 'This game is like Star Trek simplified. When I was a kid, I played it enthusiastically on my father's PC-8001. 
108 'At the time, I had never heard of the Star Trek game.
109 '
110 'YouTube: https://www.youtube.com/channel/UC8-RK6KfuLMHC85hlQfQzxg
111 'X(Twitter): https://twitter.com/KushikatsuKushi
112 '##############################################################################################################
1000 WIDTH 40,25
1010 SFX=4: SFY=2: '#### scalling factor 160x100->640x200
1020 SFX2=8: SFY2=2: '#### scalling factor 80x100->640x200
1030 CONSOLE 0,25,0,0
1040 PRINT CHR$(12):WIDTH 40,25:COLOR 7
1050 LOCATE 4,0:PRINT "....Space Battleship Yamato...."
1060 FOR I=1 TO 300:NEXT
1070 DIM A%(44),B%(44),C%(44),J(10),X(10),Y(10),A(3)
1080 COLOR 5
1090 LOCATE 3,5:PRINT CHR$(&HE6)+CHR$(&H86)+CHR$(&H85)+CHR$(&H84)+" "+CHR$(&HEF)+CHR$(&H87)+CHR$(&HEE)
1100 LOCATE 3,6:PRINT  " "+CHR$(&HE6)+CHR$(&H83)+CHR$(&H87)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+" Yamato"
1110 LOCATE 3,7:PRINT  "        "+CHR$(&H94)
1120 COLOR 2
1130 LOCATE 3,7:PRINT CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&HE7)
1140 COLOR 6
1150 LOCATE 3,10:PRINT " "+CHR$(&HE3)+" "+CHR$(&HE4)+CHR$(&H84)+CHR$(&HE5)+" "+CHR$(&HE1)
1160 LOCATE 3,11:PRINT CHR$(&HE4)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&HE5)+" Gamilas
1170 LOCATE 3,12:PRINT CHR$(&HE6)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&HE7)
1180 COLOR 7
1190 LOCATE 3,15:PRINT " "+CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)
1200 LOCATE 3,16:PRINT CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)+" Asteroid"
1210 LOCATE 3,17:PRINT " "+CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)+" "+CHR$(&HF0)
1220 GET@A(3, 5)-(12, 8),A%
1230 GET@A(3,10)-(12,13),B%
1240 GET@A(3,15)-(12,18),C%
1250 FOR I=1 TO 300:NEXT
1260 WA=0:DY=365:DA=0:DG=0
1270 LOCATE 22, 6:PRINT"Command List";
1280 LOCATE 22, 8:PRINT"1. Long Warp";
1290 LOCATE 22, 9:PRINT"2. Main Gun";
1300 LOCATE 22,10:PRINT"3. Wave Motion Gun";
1310 LOCATE 22,11:PRINT"4. Information";
1320 LOCATE 22,12:PRINT"5. Short Warp";
1330 LOCATE 22,13:PRINT"6. Repair";
1340 LOCATE  0,22:PRINT"Yamato to Iskandar!..Hit Return Key"
1350 A$=INPUT$(1):IF A$<>CHR$(13) THEN 1350
1360 PRINT CHR$(12):WIDTH 40,25
1370 COLOR 2:GOSUB 4400
1380 COLOR 7:LOCATE 10,15
1390 PRINT"Earth"
1400 PUT@A(5,5)-(14,8),A%
1410 FOR I=1 TO 2000:NEXT
1420 WIDTH 80,25
1430 GOTO 1760
1440 COLOR 7:LOCATE 2,23:BEEP
1450 PRINT "Captain, your order(1-6)?"
1460 INPUT A
1470 IF A=1 THEN 1760
1480 IF A=2 THEN 2240
1490 IF A=3 THEN 2370
1500 IF A=4 THEN GOSUB 2890:GOSUB 3600:GOTO 1440
1510 IF A=6 THEN 3370
1520 IF A=5 THEN 3040
1530 GOSUB 3600:GOTO 3700
1540 FOR M=1 TO K
1550 P=COS(A(M)/180*3.1415):Q=-SIN(A(M)/180*3.1415)
1560 H=0:V=0:W=0
1570 H=H+5
1580 V=P*H+(X(I)+5)*2:W=Q*H+Y(I)*4
1590 IF V<0 OR V>155 THEN 1720
1600 IF W<0 OR W>95 THEN1720
1610 COLOR 7:BEEP 1:PSET(SFX*V,SFY*W):BEEP 0
1620 N=0
1630 N=N+1 :IF N>10 THEN 1570
1640 IF N=I OR J(N)=O THEN 1630
1650 IF V<(X(N)+1)*2 OR V>(X(N)+8)*2 THEN 1630
1660 IF W<Y(N)*4 OR W>(Y(N)+3)*4 THEN 1630
1670  LINE (SFX*((X(I)+6)*2 ),SFY*(Y(I)*4 ))-(SFX*(V                ),SFY*(W               )),PRESET
1680 GOSUB 3610:GOSUB 3970:GOTO 1710
1690 DA=DA+1
1700 GOSUB 4040
1710 GOSUB 3610:GOTO 1740
1720 GOSUB 3600
1730 GOSUB 3610
1740 NEXT M
1750 RETURN
1760 IF DA<17 THEN 1790
1770 WIDTH 40,25:LOCATE 10,10:PRINT "Warp engine disabled!"
1780 GOTO 1440
1790 WA=WA+1:DA=DA+2
1800 DY=DY-14:HA=0
1810 COLOR3,0:FORI=0TO40STEP5
1820  LINE (SFX*(80-2*I       ),SFY*(40-I        ))-(SFX*(80+2*I           ),SFY*(40+I            )),PSET,3,B
1830 NEXT I:COLOR7,0
1840 PRINT CHR$(12)
1850 SYMBOL (0,50), "Warp", 20, 10, 7 :' For English
1860 'FOR I=0 TO10
1870 'LINE (SFX*(10+I         ),SFY*(20 ))-(SFX*(10+I             ),SFY*(40 )),PSET
1880 'LINE (SFX*(20 ),SFY*(20+I        ))-(SFX*(40 ),SFY*(20+I            )),PSET
1890 'LINE (SFX*(40+I         ),SFY*(20 ))-(SFX*(40+I             ),SFY*(40 )),PSET
1900 'LINE (SFX*(40+I         ),SFY*(40 ))-(SFX*(20+I             ),SFY*(60 )),PSET
1910 'LINE (SFX*(60 ),SFY*(30+I        ))-(SFX*(90 ),SFY*(30+I            )),PSET
1920 'LINE (SFX*(100 ),SFY*(20+I        ))-(SFX*(130 ),SFY*(20+I            )),PSET
1930 'LINE (SFX*(120+I        ),SFY*(30 ))-(SFX*(120+I            ),SFY*(40 )),PSET
1940 'LINE (SFX*(120+I        ),SFY*(40 ))-(SFX*(100+I            ),SFY*(60 )),PSET
1950 'NEXT I
1960 'LINE (SFX*(135 ),SFY*(5 ))-(SFX*(150 ),SFY*(20 )),PSET,7,BF
1970 'LINE (SFX*(140 ),SFY*(10 ))-(SFX*(145 ),SFY*(15 )),PRESET,7,BF
1980 IF WA=10 THEN 4580
1990 IF WA<10 THEN 2010
2000 IF RND(1)<.6 THEN 4230
2010 Q=50*(J(6)+J(7)+J(8)+J(9)+J(10)):DY=DY-Q
2020 IF Q=0 THEN 2070
2030 IF DY<0 THEN 4190
2040 PRINT CHR$(12):WIDTH 40,25:COLOR7
2050 PRINT "Surviving Gamilas attacks Earth! The destruction of the earth was hastened by ";Q;"days."
2060 GOSUB 2900:GOTO 2090
2070 DG=DA:FOR DA=1 TO DG:GOSUB 4040:NEXT
2080 DA=DG:WIDTH 80,25
2090 FOR I=1 TO 10
2100 X(I)=INT(RND(1)*8)*9:Y(I)=INT(RND(1)*7)*3
2110 IF I=1 THEN 2150
2120 FOR H=1 TO I-1
2130 IF X(I)=X(H) AND Y(I)=Y(H) THEN 2100
2140 NEXT H
2150 NEXT I
2160 P=INT(RND(1)*4)+7
2170 FOR I=1 TO10
2180 IF I=<P THEN 2200
2190 J(I)=0:GOTO 2210
2200 J(I)=1
2210 NEXT I
2220 GOSUB 3600
2230 GOTO 3700
2240 PRINT CHR$(12):WIDTH 40,25:COLOR 7
2250 IF DA<9 THEN 2290
2260 BEEP:LOCATE 10,10:PRINT "Main gun unabled!"
2270 FOR I=1 TO 2000:NEXT
2280 GOSUB 3600:GOTO 1440
2290 INPUT"Vertical angle of main gun 1";A(1)
2300 IF DA>=7 THEN K=1:GOTO 2350
2310 INPUT"Vertical angle of main gun 2";A(2)
2320 IF DA>=5 THEN K=2:GOTO 2350
2330 INPUT"Vertical angle of main gun 3";A(3)
2340 K=3
2350 GOSUB 3600:I=1
2360 GOSUB 1540:GOTO 3700
2370 PRINT CHR$(12):WIDTH 40,25
2380 COLOR 7:IF DA<12 AND HA<>1 THEN 2420
2390 LOCATE 10,10:PRINT "Wave motion gun unable!"
2400 FOR I=1 TO 3000:NEXT
2410 GOSUB 3600:GOTO 1440
2420 LOCATE 10, 5:PRINT "...Wave motion gun..."
2430 LOCATE  5, 7:PRINT "What is direction of wave motion gun?"
2440 LOCATE 10, 9:PRINT "Top......(1)"
2450 LOCATE 10,10:PRINT "Front....(2)"
2460 LOCATE 10,11:PRINT "Bottom...(3)"
2470 LOCATE  5,15:PRINT "Any number you like!"
2480 INPUT F:GOSUB 3600:COLOR 7
2490 IF F=1 THEN HA=1:GOTO 2530
2500 IF F=2 THEN HA=1:GOTO 2660
2510 IF F=3 THEN HA=1:GOTO 2770
2520 GOSUB 3600:GOTO 3700
2530 FOR I=X(1)*2 TO 0 STEP -5
2540 BEEP 1
2550  LINE (SFX*(X(1)*2 ),SFY*((Y(1)+1)*4 ))-(SFX*(I                ),SFY*(0 )),PSET
2560 BEEP 0:NEXT I
2570 FOR I=0 TO (Y(1)+1)*4 STEP 5
2580 BEEP 1:COLOR 7
2590  LINE (SFX*(X(1)*2 ),SFY*((Y(1)+1)*4 ))-(SFX*(0 ),SFY*(I               )),PSET
2600 BEEP 0:NEXT I
2610 FOR N=2 TO 10
2620 IF J(N)=0 THEN 2650
2630 IF X(N)>=X(1) OR Y(N)>Y(1)+1 THEN 2650
2640 J(N)=0:GOSUB 3970
2650 NEXT N:GOTO 2520
2660 DF=Y(1)*4-25
2670 FOR I=DF TO DF+50 STEP 5:BEEP1
2680 IF I<0 OR I>100 THEN 2700
2690  LINE (SFX*(0 ),SFY*(I           ))-(SFX*(X(1)*2 ),SFY*(I               )),PSET
2700 BEEP 0:NEXT I
2710 FOR N=2 TO 10
2720 IF J(N)=0 THEN 2750
2730 IF X(N)>=X(1) OR Y(N)<Y(1)-6 OR Y(N)>Y(1)+6 THEN 2750
2740 J(N)=0:GOSUB 3970
2750 NEXT N
2760 GOTO 2520
2770 FOR I=Y(1)*4 TO 100 STEP 5:BEEP1
2780  LINE (SFX*(X(1)*2 ),SFY*((Y(1)+1)*4 ))-(SFX*(0 ),SFY*(I               )),PSET
2790 BEEP 0:NEXT I
2800 FOR I=0 TO X(1)*2 STEP 5 :BEEP1
2810  LINE (SFX*(X(1)*2 ),SFY*((Y(1)+1)*4 ))-(SFX*(I                ),SFY*(100 )),PSET
2820 BEEP 0:NEXT I
2830 FOR N=2 TO 10
2840 IF J(N)=0 THEN 2870
2850 IF X(N)>=X(1) OR Y(N)<Y(1)+1 THEN 2870
2860 J(N)=0:GOSUB 3970
2870 NEXT N
2880 GOTO 2520
2890 PRINT CHR$(12):WIDTH 40,25:COLOR 6
2900 LOCATE 3,3:PRINT DY;"days to the end of humanity."
2910 COLOR 7:IF WA>10 THEN 2930
2920 LOCATE 3,4:PRINT 10-WA;"more warps to reach Iskandar."
2930 LOCATE 3,6:PRINT "(1)Long Warp  (4)Information
2940 LOCATE 3,7:PRINT "(2)Main Gun   (5)Short Warp
2950 PRINT"   (3)Wave Motion Gun  (6)Repair
2960 LOCATE 0,10:IF DA=0 THEN 3010
2970 DG=DA
2980 FOR DA=1 TO DG
2990 GOSUB 4040:NEXT
3000 DA=DG
3010 PRINT "Hit Return Key             "
3020 A$=INPUT$(1):IF A$<>CHR$(13) THEN 3020
3030 RETURN
3040 PRINT CHR$(12):WIDTH 40,25:COLOR 7
3050 IF DA<15 THEN 3090
3060 LOCATE 10,10:PRINT "Short warp unabled!"
3070 FOR I=1 TO 1500:NEXT
3080 GOSUB 3600:GOTO 1440
3090 PRINT "Direction is entered by ange. Distance is 80x80 the size of the screen. When Yamato flies out of the screen, it goes into a long warp."
3100 LOCATE 10,5:INPUT"Direction of warp";E
3110 LOCATE 10,8:INPUT"Distance of warp";F
3120 GOSUB 3600
3130 FOR I=7 TO 0 STEP -1
3140 COLOR I:BEEP
3150 LOCATE X(1),Y(1)  :PRINT CHR$(&HE6)+CHR$(&H86)+CHR$(&H85)+CHR$(&H84)+" "+CHR$(&HEF)+CHR$(&H87)+CHR$(&HEE)
3160 LOCATE X(1),Y(1)+1:PRINT  " "+CHR$(&HE6)+CHR$(&H83)+CHR$(&H87)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)+CHR$(&H83)+CHR$(&H87)
3170 LOCATE X(1),Y(1)+2:PRINT CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&H87)+CHR$(&HE7)
3180 NEXT I
3190 X(1)=X(1)+F*COS(E/180*3.1414)
3200 Y(1)=Y(1)-.3125*F*SIN(E/180*3.1415)
3210 PUT@A(X(1),Y(1))-(X(1)+9,Y(1)+3),A%
3220 ON ERROR GOTO 1760
3230 IF X(1)>71 OR X(1)<0 THEN 1760
3240 IF Y(1)>22 OR Y(1)<0 THEN 1760
3250 FOR N=2 TO 10
3260 IF J(N)=0 THEN 3340
3270 IF X(1)>X(N)+9 OR X(1)+9<X(N) THEN 3340
3280 IF Y(1)>Y(N)+3 OR Y(1)+3<Y(N) THEN 3340
3290 GOSUB 3970
3300 DG=DA
3310 FOR DA=DG TO DG+5
3320 GOSUB 4040:NEXT DA
3330 DA=DG+5
3340 NEXT N
3350 GOSUB 3610
3360 GOTO 3700
3370 PRINT CHR$(12):WIDTH 40,25:COLOR 7
3380 LOCATE 10,10:PRINT"Repairing..."
3390 FOR I=1 TO 1000:NEXT
3400 DY=DY-DA*3
3410 LOCATE 10,12:PRINT "Repair takes";DA*3;"days..."
3420 IF DY<0 THEN 4190
3430 FOR I=1 TO 3000:NEXT
3440 COLOR6:LOCATE 10,10:BEEP
3450 PRINT"Repair complete!!            "
3460 LOCATE 10,12:PRINT "Repair took";DA*3;"days."
3470 DA=0:DG=0
3480 FOR I=1 TO 2000:NEXT
3490 GOSUB 2900
3500 GOSUB 3600
3510 GOTO 3700
3520 PRINT CHR$(12):WIDTH 40,25
3530 LOCATE 5,5:COLOR 6
3540 PRINT "The earth was saved!!"
3550 LOCATE 3,8:COLOR 7
3560 PRINT "The people of the earth will remember you forever."
3570 LOCATE 15,11:PRINT "You won!!"
3580 LOCATE 15,12:PRINT DY;"days left."
3590 GOTO 4490
3600 PRINT CHR$(12):WIDTH 80,25
3610 FOR Z=10 TO 1 STEP -1
3620 IF J(Z)=0 THEN 3680
3630 IF Z=1 THEN 3670
3640 IF Z>=6 THEN 3660
3650 PUT@A(X(Z),Y(Z))-(X(Z)+9,Y(Z)+3),C%:GOTO 3680
3660 PUT@A(X(Z),Y(Z))-(X(Z)+9,Y(Z)+3),B%:GOTO 3680
3670 PUT@A(X(Z),Y(Z))-(X(Z)+9,Y(Z)+3),A%
3680 NEXT Z
3690 RETURN
3700 S=INT(RND(1)*5)+5
3710 IF S=5 THEN1440
3720 FOR QW=6 TO S
3730 IF J(QW)=0 THEN 3950
3740 K=1
3750 K=K+1:IF K=11 THEN 3840
3760 IF J(K)=0 OR K=QW THEN 3750
3770 X1=X(QW)-X(K):Y1=Y(QW)-Y(K)
3780 X2=X(QW)-X(1):Y2=Y(QW)-Y(1)
3790 IF X1=0 AND X2=0 THEN 3820
3800 IF X1=0 OR X2=0 THEN 3750
3810 IF (Y1/X1)<>(Y2/X2) THEN 3750
3820 IF Y1*Y2<0 THEN 3750
3830 GOTO 3900
3840 BEEP 1:COLOR 7
3850  LINE (SFX*(X(QW)*2 ),SFY*(Y(QW)*4 ))-(SFX*((X(1)+2)*2 ),SFY*(Y(1)*4 )),PSET
3860  LINE (SFX*(X(QW)*2 ),SFY*(Y(QW)*4 ))-(SFX*((X(1)+2)*2 ),SFY*(Y(1)*4 )),PRESET
3870 BEEP 0:GOSUB 3610
3880 N=1:GOSUB 3970:J(1)=1
3890 GOSUB 3610:DA=DA+1:GOSUB 4040:GOTO 3950
3900 BEEP 1:COLOR 7
3910  LINE (SFX*(X(QW)*6 ),SFY*(Y(QW)*4 ))-(SFX*((X(K)+4.5)*2 ),SFY*((Y(K)+1.5)*4 )),PSET
3920  LINE (SFX*(X(QW)*2 ),SFY*(Y(QW)*4 ))-(SFX*((X(K)+4.5)*2 ),SFY*((Y(K)+1.5)*4 )),PRESET
3930 BEEP 0:GOSUB 3610
3940 N=K:GOSUB 3970
3950 NEXT QW
3960 GOTO 1440
3970 COLOR2,0
3980 FOR U=1 TO 50:BEEP 0
3990  LINE (SFX*((X(N)+4.5)*2 ),SFY*((Y(N)+1.5)*4))-(SFX*(X(N)*2+RND(1)*18 ),SFY*(Y(N)*4+RND(1)*12)),PSET
4000 BEEP 1
4010 NEXT U:BEEP 0
4020  LINE (SFX*(X(N)*2 ),SFY*(Y(N)*4 ))-(SFX*((X(N)+9)*2 ),SFY*((Y(N)+3)*4 )),PRESET,7,BF
4030 J(N)=0:RETURN
4040 COLOR 7
4050 IF DA=5 THEN 4130
4060 IF DA=7 THEN 4140
4070 IF DA=9 THEN 4150
4080 IF DA=12 THEN 4160
4090 IF DA=15 THEN 4170
4100 IF DA=17 THEN 4180
4110 IF DA=20 THEN 4680
4120 RETURN
4130 BEEP:PRINT "Main gun 3 unabled!":FOR I=1 TO 2000:NEXT:RETURN
4140 BEEP:PRINT "Main gun 2 unabled!":FOR I=1 TO 2000:NEXT:RETURN
4150 BEEP:PRINT "Main gun 1 unabled!":FOR I=1 TO 2000:NEXT:RETURN
4160 BEEP:PRINT "Wave motion gun unabled!":FOR I=1 TO 2000:NEXT:RETURN
4170 BEEP:PRINT "Short warp unabled!":FOR I=1 TO 2000:NEXT:RETURN
4180 BEEP:PRINT "Warp engine unabled!":FOR I=1 TO 2000:NEXT:RETURN
4190 PRINT CHR$(12):WIDTH 40,25
4200 COLOR 7:BEEP
4210 LOCATE 15,5:PRINT "Yamato did not make it.."
4220 GOTO 4460
4230 IF DY<0 THEN 4190
4240 IF DA>=20 THEN 4680
4250 PRINT CHR$(12):WIDTH 40,25
4260 COLOR 2
4270 GOSUB 4400
4280 COLOR 7:LOCATE 5,20:PRINT "Earth"
4290 PUT@A(30,14)-(39,17),A%
4300 FOR I=1 TO 2000:NEXT
4310 FOR I=0 TO 20
4320 PUT@A(30-I,14)-(39-I,17),A%
4330 LINE (SFX*(40-I         ),SFY*(17 ))-(SFX*(40-I             ),SFY*(14 )),PRESET
4340 NEXT I
4350 FOR I=1 TO 2000:NEXT
4360 COLOR 4
4370 GOSUB 4400
4380 FOR I=1 TO 3000:NEXT
4390 GOTO 3520
4400 PI=3.1415
4410 FOR I=0 TO 2*PI STEP.05
4420 XX=SIN(I)*23+40:YY=COS(I)*35+55
4430 LINE (SFX2*(XX           ),SFY2*(YY          ))-(SFX2*(40 ),SFY2*(55 )),PSET
4440 NEXT
4450 RETURN
4460 LOCATE 4,7:PRINT "In spite of your efforts, humanity perished...
4470 COLOR 6:LOCATE 15,10
4480 PRINT "You lost!"
4490 LOCATE 15,12:PRINT DY;"days left."
4500 COLOR 7
4510 LOCATE 10,18:PRINT "Play again(Y/N)?"
4520 A$=INKEY$:IFA$=" " THEN4520
4530 IF A$="n" THEN PRINT "N":END
4540 IF A$="y" THEN 4560
4550 GOTO 4520
4560 ERASE J,X,Y,A,A%,B%,C%
4570 GOTO 1000
4580 PRINT CHR$(12):WIDTH 40,25
4590 COLOR 4:GOSUB 4400
4600 COLOR 7:LOCATE 10,15:PRINT "Iskandar"
4610 PUT@A(25,5)-(34,8),A%
4620 LOCATE 5,20:PRINT "Arrived at Iskandar."
4630 FOR I=1 TO 3500:NEXT
4640 LOCATE 5,20:BEEP:PRINT "Finished loading Cosmo Cleaner!!"
4650 FOR I=1 TO 4000:NEXT
4660 WIDTH 80,25
4670 GOTO 1760
4680 PRINT CHR$(12):WIDTH 40,25
4690 X(1)=15:Y(1)=11
4700 PUT@A(X(1),Y(1))-(X(1)+9,Y(1)+3),A%
4710 FOR I=1 TO 1000:NEXT
4720 N=1:GOSUB 3970
4730 COLOR 7:LOCATE 15,5
4740 PRINT "Yamato lost!!"
4750 GOTO 4460
